number = {      
    ASCII_DIGIT+
}

letter = { 'a'..'z' | 'A'..'Z' }

scheme = { 
    ^"file"
    | ^"ftp"
    | ^"http"
    | ^"https"
    | ^"imap"
    | ^"irc"
    | ^"nntp"
    | ^"acap"   
    | ^"icap"   
    | ^"mtqp"
    | ^"wss"
}

word = { letter+ }

word_with_numbers= { (letter | ASCII_DIGIT)+ }

// label is a part of domain name separated by [.]
label = ${
    word ~ ("-" ~ word_with_numbers)*
}

domain_name = ${
	label ~ ("." ~ label)* 
}

port = {
    ASCII_DIGIT{1, 6}
}

paths = ${
    ("/" ~ word_with_numbers)+
}

key_value_pair = {
    word_with_numbers ~ "=" ~ word_with_numbers
}

parameters = {
    key_value_pair ~ ("&" ~ key_value_pair)*
}

fragment = {
    word_with_numbers
}

url = {
    SOI ~ scheme ~ "://" ~ domain_name ~ (":" ~ port)? ~ (paths)? ~ ("?" ~ parameters)? ~ ("#" ~ fragment)? ~ EOI
}


WHITESPACE = _{ " " }
